fn true(ifTrue ifFalse) ifTrue()

fn false(ifTrue ifFalse) ifFalse()

fn nil(ifNil) ifNil()

fn some(a) fn(ifNil ifSome) ifSome(a)

fn cons(head tail) fn(ifNil ifCons) ifCons(head tail)

fn head(list) list(fn() nil fn(head tail) head)

fn tail(list) list(fn() nil fn(head tail) tail)

fn map(list f) {
    list(
        fn() nil
        fn(head tail) cons(f(head) map(tail f))
    )
}

fn fold(list acc f) {
    list(
        fn() acc
        fn(head tail) fold(tail f(acc head) f)
    )
}

fn unfold(state f) {
    fn go(acc state) {
        f(state)(
            fn() acc
            fn(value state*) go(cons(value acc) state*)
        )
    }(nil state)
}

fn strToInt(s) {
    fn go(s) fold(s 0 fn(acc d) add(mul(acc 10) sub(d '0')))
    eq?(head(s) '-')(
        fn() neg(go(tail(s)))
        fn() go(s)
    )
}

fn intToStr(n) {
    fn posIntToStr(n) unfold(n fn(n) {
        zero?(n)(
            fn() nil
            fn() cons(add(rem(n 10) '0') div(n 10))
        )
    })
    zero?(n)(
        fn() "0"
        fn() neg?(n)(
            fn() cons('-' posIntToStr(neg(n)))
            fn() posIntToStr(n)
        )
    )
}
